;+
; NAME:
;    jansen_menu_event
;
; PURPOSE:
;    Handle events generated by the Jansen menu bar
;
; MODIFICATION HISTORY:
; 02/13/2015 Written by David G. Grier, New York University
;
; Copyright (c) 2015 David G. Grier
;-

pro jansen_menu_event, event

  COMPILE_OPT IDL2, HIDDEN

  widget_control, event.top, get_uvalue = state
  video = state['video']

  widget_control, event.id, get_uvalue = uval
  case uval of
     'SNAPSHOT': video.saveimage
     'RECORD': begin
        if ~video.recording then begin
           filename = video.filename
           fn = dialog_pickfile(title = 'Jansen Start Recording', $
                                file = filename, default_extension = '.h5', $
                                /write, /overwrite_prompt, $
                                filter = '*.h5', /fix_filter, $
                                path = video.directory, $
                                resource_name = 'Jansen')
           if (fn.length gt 0) then begin
              video.filename = fn
              video.recording = 1
           endif
        endif else $
           video.recording = 0
        widget_control, event.id, $
                        set_value = (~video.recording) ? 'Record' : 'Stop Recording!'
     end
     else: print, uval
  endcase
  
end
